mapping = _{ SOI ~ assignment ~ (NEWLINE+ ~ assignment)* ~ EOI }

assignment = { target_path ~ "=" ~ query }

// Paths
path_segment = @{ ('a'..'z' | 'A'..'Z' | "_" | "-" )+ ~ ( "[" ~ ('0'..'9')+ ~ "]" )? }

target_path = @{ ("." ~ path_segment)+ }

// Queries
path_coalesce = !{ "(" ~ path_segment ~ ("|" ~ path_segment)+ ~ ")" }

dot_path = ${ ("." ~ (path_segment | path_coalesce))+ }

query = _{ value | dot_path }

value = _{ string | number | boolean | null }

boolean = { "true" | "false" }

null = { "null" }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

WHITESPACE = _{ " " | "\t" }